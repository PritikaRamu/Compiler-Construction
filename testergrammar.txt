<program>  -> <otherFunctions> <mainFunction> 

<mainFunction>  -> TK_MAIN <stmts> TK_END

<otherFunctions>  -> <eps>
<otherFunctions>  -> <function> <otherFunctions>

<function>  -> TK_FUNID <input_par> <output_par> TK_SEM <stmts> TK_END

<input_par>  -> TK_INPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR

<output_par>  -> <eps>
<output_par>  -> TK_OUTPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR

<parameter_list>  -> <dataType> TK_ID <remaining_list>

<dataType>  -> <constructedDatatype>
<dataType>  -> <primitiveDatatype>

<primitiveDatatype>  -> TK_REAL
<primitiveDatatype>  -> TK_INT

<constructedDatatype>  -> TK_RUID
<constructedDatatype>  -> TK_UNION TK_RUID
<constructedDatatype>  -> TK_RECORD TK_RUID

<remaining_list>  -> <eps>
<remaining_list>  -> TK_COMMA <parameter_list>

<stmts>  -> <typeDefinitions> <declarations> <otherStmts> <returnStmt>

<typeDefinitions>  -> <eps>
<typeDefinitions>  -> <actualOrRedefined> <typeDefinitions>

<typeDefinition>  -> TK_UNION TK_RUID<fieldDefinitions> TK_ENDUNION
<typeDefinition>  -> TK_RECORD TK_RUID<fieldDefinitions> TK_ENDRECORD

<fieldDefinitions>  -> <fieldDefinition> <fieldDefinition> <moreFields>

<fieldDefinition>  -> TK_TYPE <fieldtype> TK_COLON TK_FIELDID TK_SEM

<moreFields>  -> <eps>
<moreFields>  -> <fieldDefinition> <moreFields>

<declarations>  -> <eps>
<declarations>  -> <declaration> <declarations>

<declaration>  -> TK_TYPE <dataType> TK_COLON TK_ID <global_or_not> TK_SEM

<global_or_not>  -> <eps>
<global_or_not>  -> TK_COLON TK_GLOBAL

<otherStmts>  -> <eps>
<otherStmts>  -> <stmt> <otherStmts>

<stmt>  -> <funCallStmt>
<stmt>  -> <ioStmt>
<stmt>  -> <conditionalStmt>
<stmt>  -> <iterativeStmt>
<stmt>  -> <assignmentStmt>

<assignmentStmt>  -> <singleOrRecId> TK_ASSIGNOP <arithmeticExpression> TK_SEM

<singleOrRecId>  -> TK_ID <option_single_constructed>

<actualOrRedefined>  -> <definetypestmt>
<actualOrRedefined>  -> <typeDefinition>

<funCallStmt>  -> <outputParameters> TK_CALL TK_FUNID TK_WITH TK_PARAMETERS <inputParameters> TK_SEM

<outputParameters>  -> <eps>
<outputParameters>  -> TK_SQL <idList> TK_SQR TK_ASSIGNOP

<inputParameters>  -> TK_SQL <idList> TK_SQR

<iterativeStmt>  -> TK_WHILE TK_OP <booleanExpression> TK_CL <stmt> <otherStmts> TK_ENDWHILE

<conditionalStmt>  -> TK_IF TK_OP <booleanExpression> TK_CL TK_THEN <stmt> <otherStmts> <elsePart>

<elsePart>  -> TK_ENDIF
<elsePart>  -> TK_ELSE <stmt> <otherStmts> TK_ENDIF

<ioStmt>  -> TK_WRITE TK_OP <var> TK_CL TK_SEM
<ioStmt>  -> TK_READ TK_OP <var> TK_CL TK_SEM

<fieldtype>  -> TK_RUID
<fieldtype>  -> <primitiveDatatype>

<option_single_constructed>  -> <oneExpansion> <moreExpansions>

<arithmeticExpression>  -> <term> <expPrime>

<expPrime>  -> <eps>
<expPrime>  -> <lowPrecedenceOperators> <term> <expPrime>

<term>  -> <factor> <termPrime>

<termPrime>  -> <eps>
<termPrime>  -> <highPrecedenceOperators> <factor> <termPrime>

<factor>  -> <var>
<factor>  -> TK_OP <arithmeticExpression> TK_CL

<highPrecedenceOperators>  -> TK_DIV
<highPrecedenceOperators>  -> TK_MUL

<lowPrecedenceOperators>  -> TK_MINUS
<lowPrecedenceOperators>  -> TK_PLUS

<oneExpansion>  -> TK_DOT TK_FIELDID

<moreExpansions>  -> <eps>

<booleanExpression>  -> TK_NOT TK_OP <booleanExpression> TK_CL
<booleanExpression>  -> <var> <relationalOp> <var>
<booleanExpression>  -> TK_OP <booleanExpression> TK_CL <logicalOp> TK_OP <booleanExpression> TK_CL

<var>  -> TK_RNUM
<var>  -> TK_NUM
<var>  -> <singleOrRecId>

<logicalOp>  -> TK_OR
<logicalOp>  -> TK_AND

<relationalOp>  -> TK_NE
<relationalOp>  -> TK_GE
<relationalOp>  -> TK_GT
<relationalOp>  -> TK_EQ
<relationalOp>  -> TK_LE
<relationalOp>  -> TK_LT

<returnStmt>  -> TK_RETURN <optionalReturn> TK_SEM

<optionalReturn>  -> <eps>
<optionalReturn>  -> TK_SQL <idList> TK_SQR

<idList>  -> TK_ID <more_ids>

<more_ids>  -> <eps>
<more_ids>  -> TK_COMMA <idList>

<definetypestmt>  -> TK_DEFINETYPE <A> TK_RUID TK_AS TK_RUID TK_SEM

<A>  -> TK_UNION
<A>  -> TK_RECORD